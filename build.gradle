buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

project.ext.buildnumber = "undefined"
if (System.getenv().BUILD_NUMBER) project.buildnumber = System.getenv().BUILD_NUMBER

version = "1.7.10-1.0.0-alpha.1-build." + project.buildnumber
group = "net.angrypixel"
archivesBaseName = 'TheBetweenlands'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

minecraft {
    version = "1.7.10-10.13.3.1365-1.7.10"
    runDir = "minecraft"
	replace '${version}', project.version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'team.json'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'team.json'
    }
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
        java { 
            srcDir 'java' 
		}
        resources { 
            srcDir 'resources' 
        }
    }
}

jar {
    classifier = 'universal'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

artifacts {
    archives sourcesJar, javadocJar, devJar
}
