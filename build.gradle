buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

project.ext.buildnumber = ''
if (System.getenv().BUILD_NUMBER) project.buildnumber = '-' + System.getenv().BUILD_NUMBER

def modid = getModIdFromJava()
version = getVersionFromJava() + project.buildnumber
group = 'net.angrypixel'
archivesBaseName = 'TheBetweenlands'
sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10'
    runDir = 'minecraft'
    replace '${version}', project.version
    replace '/*!*/true/*!*/', 'false'
}

sourceSets {
	//Universal build
    main {
        output.resourcesDir = output.classesDir
        java { 
            srcDir 'java'
        }
        resources { 
            srcDir 'resources'
        }
    }
}

//Processes the resources of sourceSets.main
processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'ilexiconn'
        url = 'http://maven.ilexiconn.net/'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

//Builds the universal jar
jar {
    classifier = 'universal'
    addManifestAttributes(manifest)
}

//Builds the source jar with all the source files of sourceSets.main
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
}

//Builds the dev jar with all the output files of sourceSets.main
task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

//Builds the server jar
task serverJar(type: Jar, dependsOn: classes) {
	dependsOn jar
	classifier = 'server'
	//sourceSets.main.output.classesDir does not work here because it contains the deobfuscated classes which we can't use
	from (zipTree(jar.archivePath)) {
	
		//A list of files or folders to be excluded from the server build
		exclude("**/assets/" + modid + "/shaders/**")
		exclude("**/assets/" + modid + "/sounds/**")
		exclude("**/assets/" + modid + "/textures/**")
		exclude("**/assets/" + modid + "/sounds.json")
		
	}
	addManifestAttributes(manifest)
}

build.finalizedBy serverJar

/*artifacts {
    archives sourcesJar, devJar
}*/

//Returns the current version by reading directly from the ModInfo.java file
String getVersionFromJava() {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String VERSION = \"";
    File file = file("java/thebetweenlands/lib/ModInfo.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }

    return "$major.$revision.$patch";
}

//Returns the mod ID by reading directly from the ModInfo.java file
String getModIdFromJava() {
    String id = "N/A";
    String prefix = "public static final String ID = \"";
    File file = file("java/thebetweenlands/lib/ModInfo.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            id = s.substring(prefix.length(), s.length() - 2);
        }
    }

    return "$id";
}

//Adds the manifest attributes to the specified Manifest
def addManifestAttributes(Manifest m) {
	m.attributes ('FMLCorePlugin': 'thebetweenlands.core.TheBetweenlandsLoadingPlugin')
    m.attributes ('FMLCorePluginContainsFMLMod': 'true')
}